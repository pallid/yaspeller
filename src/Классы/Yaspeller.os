#Использовать json

#Область ОписаниеПеременных

Перем АдресСервиса;
Перем ПортСервиса;
Перем АдресСервисаПоУмолчанию;
Перем ПортСервисаПоУмолчанию;

Перем ПарсерJSON;

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ПриСозданииОбъекта()
	
	УстановитьПараметрыПодключения(АдресСервисаПоУмолчанию, ПортСервисаПоУмолчанию);
	
КонецПроцедуры // ПриСозданииОбъекта()

Процедура УстановитьПараметрыПодключения(пАдресСервиса, пПортСервиса) Экспорт
	АдресСервиса = пАдресСервиса;
	ПортСервиса = пПортСервиса;
КонецПроцедуры // УстановитьПараметрыПодключения()

// ПроверитьТекст
//	Возвращает массив ошибок с описаниями
//
// Возвращаемое значение:
//   Массив   - Массив объектов SpellResult
//
Функция ПроверитьТекст(Текст) Экспорт

	Соединение = Новый HTTPСоединение(АдресСервиса, ПортСервиса);		
	Заголовки = Новый Соответствие;
    Заголовки.Вставить("Content-Type", "application/x-www-form-urlencoded"); 
    Запрос = Новый HTTPЗапрос("/services/spellservice.json/checkText", Заголовки);
    Запрос.УстановитьТелоИзСтроки("text="+Текст);

	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	Если Ответ.КодСостояния = 200 Тогда
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		ДанныеОтвета = ПарсерJSON.ПрочитатьJSON(ТелоОтвета);
		Если ДанныеОтвета = Неопределено Тогда	
			ТекстОшибки = ТелоОтвета;
			Сообщить(ТекстОшибки, СтатусСообщения.Важное);
		Иначе
			МассивРезультатовПроверки = ПолучитьМассивРезультатовПроверки(ДанныеОтвета);
		КонецЕсли;
	Иначе
		Сообщить("ответ сервиса: " + Ответ.КодСостояния, СтатусСообщения.Важное);
		Сообщить("тело ответа: " + Ответ.ПолучитьТелоКакСтроку(), СтатусСообщения.Важное);
	КонецЕсли;
	
	Возврат МассивРезультатовПроверки;

КонецФункции

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция ПолучитьМассивРезультатовПроверки(Данные)

	Массив = Новый Массив;

	Для Каждого СтрокаДанных Из Данные Цикл
		Ошибка = Новый SpellResult(СтрокаДанных);
		Массив.Добавить(Ошибка);
	КонецЦикла;	
	
	Возврат Массив;

КонецФункции

Функция ПолучитьСтруктуруОписанияОшибки()

	Возврат Новый Структура("КодОшибки, Позиция, Строка, Столбец, Длина, ИсходноеСлово, Подсказки");

КонецФункции

#КонецОбласти

АдресСервисаПоУмолчанию = "https://speller.yandex.net";
ПортСервисаПоУмолчанию = "443";
ПарсерJSON = Новый ПарсерJSON();